## Reconstruccion filogenetica de Arctostaphylos sin incluir reloj molecular

## datos
datos <- readDiscreteCharacterData("/home/dane/MEGAsync/bio_comparadaII/Goldberg/data/Arctostaphylos/NEXUS/Arctostaphylos.nex")

## Numero de taxa
ntax <- datos.ntaxa()

## taxa

taxa <- datos.taxa()

# numero de ramas

n_branches <- 2 * ntax - 3

# set my move index
mvi = 0
mni = 0

## Modelo de substituciÃ³n K80 + G

#K80 (Es necesario el parametros kappa= tasa de transicion transversion. El cual fue extraido de la salida de jmodeltest
# Linea 230)

# kappa = 2.5499 (ti/tv=1.2750)

kappa ~ dnLnorm(2.5499, 1.2750)

moves[++mvi] = mvScale(kappa)

Q := fnK80(kappa)

# Gamma

# segun el manual de revbayes
# alpha_prior_mean <- 5.0
# alpha_prior_sd <- 0.587405
# alpha ~ dnLognormal( alpha_prior_mean, alpha_prior_sd )

#alphaprior
# gamma shape obtenido de jmodeltest 0.1300 linea 1312

shape_prior <- 0.1300
prior_sd <-0.5
shape  ~ dnLognormal(shape_prior, prior_sd)

# inicio nodo deterministico
gamma_rates := fnDiscretizeGamma( shape, 1/shape, 4 )

# Escala del movimiento del parametro
moves[++mvi] = mvScale(shape, weight=2.0)

# Tree prior
topology ~ dnUniformTopology(taxa=taxa)

moves[++mvi] = mvNNI(topology, weight=3.0)

moves[++mvi] = mvSPR(topology, weight=3.0)


## Prior longitud de ramas exponencial

for(i in 1:n_branches){
	br_lens[i] ~ dnExponential(10.0)
	moves[++mvi] = mvScale(br_lens[i])
}

TL := sum(br_lens)

phylogeny := treeAssembly(topology, br_lens)

##

seq ~ dnPhyloCTMC(tree=phylogeny,siteRates=gamma_rates, Q=Q, type="DNA")

seq.clamp(datos)

mymodel = model(seq)

## Monitores

monitors[++mni] = mnModel(filename="output/Arctostaphylos_K80.log", printgen=1000)
monitors[++mni] = mnFile(filename="output/Arctostaphylos_K80.trees", printgen=1000, phylogeny)
monitors[++mni] = mnScreen(printgen=1000, TL)

## MCMC
mymcmc = mcmc(mymodel, monitors, moves)

mymcmc.run(generations=4000000)

treetrace = readTreeTrace("output/Arctostaphylos_K80.trees", treetype="non-clock")

map_tree = mapTree(treetrace,"output/Arctostaphylos_K80_MAP.tree")

# exit
quit()
